Language "IBAFlang"

# Statements

Syntax Stmt: statement ::= block
                         | 'print' '(' exp ')' ';'
                         | 'int' id ';'
                         | id '=' exp ';'
                         | 'int' id '=' exp ';'
                         | 'return' exp? ';'
                         | 'fun' id '(' params ')' block

Syntax Block: block ::= '{' statement* '}'                    

Syntax Params: params ::= id (',' params)?

Semantics execute[[ Stmt*:statement* ]] : => null-type

Rule execute[[ Block ]] = execute-block[[ Block ]]
Rule execute[[ 'print' '(' Exp ')' ';' ]] = print eval-exp[[ Exp ]]
Rule execute[[ 'int' Id ';' ]] = assign(bound id[[ Id ]], 0)
Rule execute[[ Id '=' Exp ';' ]] = assign(bound id[[ Id ]], eval-exp[[ Exp ]])
Rule execute[[ 'int' Id '=' Exp ';' ]] = assign(bound id[[ Id ]], eval-exp[[ Exp ]])
Rule execute[[ 'return' Exp ';' ]] = return eval-exp[[ Exp ]]
Rule execute[[ 'return' ';' ]] = return null
Rule execute[[ 'fun' Id '(' Params ')' Block ]] = null

Rule execute[[ ]] = null
Rule execute[[ Stmt Stmt+ ]] = sequential(execute[[ Stmt ]], execute[[ Stmt+ ]])

Semantics execute-block[[ _:block ]] : => null-type
Rule execute-block[[ '{' Stmt* '}' ]] = scope(collateral(collect-declared-vars[[ Stmt* ]]), execute[[ Stmt* ]])



# Handling parameter declarations

Semantics collect-params[[ Params:params ]] : (=>environments)+
Rule collect-params[[ Id ]] = bind(
        id[[ Id ]], 
        allocate-initialised-variable(integers, checked head given)
    )
Rule collect-params[[ Id ',' Params ]] = collect-params[[ Id ]], give(checked tail given, collect-params[[ Params ]])

# Handling variable declarations

Semantics collect-declared-vars[[ Stmt*:statement* ]] : (=>environments)+

Rule collect-declared-vars[[ 'int' Id '=' Exp ';' ]] = bind(id[[ Id ]], allocate-variable(integers))
Rule collect-declared-vars[[ 'int' Id ';' ]] = bind(id[[ Id ]], allocate-variable(integers))
Rule collect-declared-vars[[ 'fun' Id '(' Params ')' '{' Stmt* '}' ]] = bind(id[[ Id ]], 
    function abstraction(
        scope(
            collateral(collect-declared-vars[[ Stmt* ]], collect-params[[ Params ]]), 
            execute[[ Stmt* ]]
        )
    )
)
Rule collect-declared-vars[[ ]] = map()
Rule collect-declared-vars[[ Stmt ]] = map()
Rule collect-declared-vars[[ Stmt1 Stmt2 Stmt* ]] = collect-declared-vars[[ Stmt1 ]], collect-declared-vars[[ Stmt2 ]], collect-declared-vars[[ Stmt* ]]












